plugins {
    id 'net.researchgate.release' version '2.6.0'
}
repositories {
    mavenCentral()
    maven {
        name 'gradle plugins'
        url 'https://plugins.gradle.org/m2/'
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}
apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'

compileGroovy.targetCompatibility = '1.8'
compileGroovy.sourceCompatibility = '1.8'

group = "com.techshroom"

dependencies {
    compile localGroovy()
    // we _would_ use jcabi-http here, but github defines all the extra deps we need desperately
    compile group: 'com.jcabi', name: 'jcabi-github', version: '0.28'
    compile group: 'net.ltgt.gradle', name: 'gradle-apt-plugin', version: '0.8'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.9.2'
}

String prepareMavenPlugin(project) {
    def data = project.file('src/main/groovy/maven/MavenPlugin.groovy').filterLine { line ->
        return !line.startsWith('package')
    };
    def ret = project.file('build/MavenPluginStripped.groovy');
    ret.parentFile.mkdirs()
    ret.newWriter().with { writer ->
        data.writeTo(writer)
    }
    return ret.toString()
}

apply from: prepareMavenPlugin(project), to: project
apply plugin: MavenPlugin
